plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    // ide
    id 'idea'
    id 'eclipse'
    // test coverage
    id 'jacoco'
    // quality
    id 'org.sonarqube' version '2.5'
    // docker build & push
    id 'maven-publish'
    id 'com.palantir.docker' version '0.13.0'
    // integration tests
    id 'org.unbroken-dome.test-sets' version '1.4.1'
}

description = 'Perform files and jobs operations with Xenon library from command line'
version = '1.0.2'
group = 'nlesc'
mainClassName = 'nl.esciencecenter.xenon.cli.Main'
applicationName = 'xenon'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://dl.bintray.com/palantir/releases' // docker-compose-rule is published on bintray, not jcenter
    }
}

testSets {
    integrationTest
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'net.sourceforge.argparse4j:argparse4j:0.7.0'
    compile 'nl.esciencecenter.xenon:xenon:1.2.2'
    compile 'org.slf4j:slf4j-api:1.7.25'

    testCompile 'junit:junit:4.12'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.1'
    integrationTestCompile 'com.github.geowarin:docker-junit-rule:1.1.0'
    integrationTestCompile 'com.palantir.docker.compose:docker-compose-rule-junit4:0.32.0'
}

docker {
    name 'hub.docker.com/nlesc/xenon-cli'
    dependsOn tasks.installShadowDist
    files tasks.installShadowDist.outputs
}
dockerfileZip.enabled = false

// use shadow dists everywhere
distZip.enabled = false
distTar.enabled = false
assemble.dependsOn shadowDistTar
assemble.dependsOn shadowDistZip

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

jacocoTestReport {
    description 'Generate coverage report of unit tests'
    group 'Code coverage reporting'
    reports {
        xml.enabled true
    }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

task jacocoIntegrationTestReport(type: JacocoReport) {
    description 'Generate coverage report of integration tests'
    group 'Code coverage reporting'
    executionData integrationTest
    sourceSets sourceSets.main
}


task jacocoMerge(type: JacocoMerge) {
    executionData test, integrationTest
}

task jacocoIntegrationAndUnitTestReport(type: JacocoReport) {
    description 'Generate coverage report of integration and unit tests'
    group 'Code coverage reporting'
    dependsOn jacocoMerge
    executionData jacocoMerge.destinationFile
    sourceSets sourceSets.main
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarqube.com'
        property 'sonar.organization', project.group
        property 'sonar.language', 'java'
        property 'sonar.links.homepage', 'https://github.com/NLeSC/xenon-cli'
        properties["sonar.tests"] += sourceSets.integrationTest.allSource.srcDirs
        property 'sonar.jacoco.reportPaths', [jacocoTestReport.executionData, jacocoIntegrationTestReport.executionData]
    }
}
project.tasks["sonarqube"].dependsOn "check"
